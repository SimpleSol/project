apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    def BOOLEAN = "boolean"
    def TRUE = "true"
    def FALSE = "false"
    def IS_DEV_SERVER = "IS_DEV_SERVER"

    defaultConfig {
        applicationId "biz.growapp.baseandroidproject"
        minSdkVersion 16
        // IF targetSdkVersion == 23, be sure to support Android 6.0 runtime-permissions and Doze-mode
        targetSdkVersion 22
        versionCode 1
        versionName "0.01"
    }

    signingConfigs {
        debug {
            storeFile file("../buildsystem/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            buildConfigField BOOLEAN, IS_DEV_SERVER, TRUE
        }
        release {
            buildConfigField BOOLEAN, IS_DEV_SERVER, FALSE
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //change default name for output build file
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            output.outputFile = new File(file.parent, defaultConfig.versionName + "-" + file.name)
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile rootProject.ext.kotlin.stdlib

    Map<String, String> network = rootProject.ext.network
    compile(
            network.retrofit,
            network.converterGson,
            network.okhttpLogging,
//            network.cookieJar
    )

    Map<String, String> android = rootProject.ext.android
    compile(
            android.design,
            android.support_all,
            android.appcompat,
            android.recyclerView,
//            android.cardView,
    )

    Map<String, String> rx = rootProject.ext.rx
    compile(
            rx.java,
            rx.android,
    )

    Map<String, String> fresco = rootProject.ext.fresco
    compile(
            fresco.fresco
    )

    Map<String, String> helpers = rootProject.ext.helpers
    compile(
            helpers.butterknife,
            helpers.threetenabp,
    )
    apt helpers.butterknife_compiler
}
